component ImportAnilist(user *arn.User, matches []*arn.AniListMatch)
	h1= "anilist.co Import (" + user.Accounts.AniList.Nick + ", " + toString(len(matches)) + " anime)"

	table.import-list
		thead
			tr
				th anilist.co
				th notify.moe
		tbody
			each match in matches
				tr
					td
						a(href=match.AniListItem.Anime.Link(), target="_blank", rel="noopener")= match.AniListItem.Anime.TitleRomaji
					td
						if match.ARNAnime == nil
							span.import-error Not found on notify.moe
						else
							a(href=match.ARNAnime.Link(), target="_blank", rel="noopener")= match.ARNAnime.Title.Canonical
	
	.buttons
		a.button.mountable(href="/import/anilist/animelist/finish")
			Icon("refresh")
			span Import