component EditLog(entries []*arn.EditLogEntry, user *arn.User)
	h1 Editor log

	table.edit-log
		thead
			tr.mountable
				th.text-center Icon
				th User
				th Object
				th Type
				th Key
				th Old
				th New
				th Date
		tbody
			each entry in entries
				tr.mountable
					td
						.edit-log-icon(title=entry.Action)
							if entry.Action == "create"
								.edit-log-create
									RawIcon("plus")
							else if entry.Action == "delete"
								.edit-log-delete
									RawIcon("minus")
							else if entry.OldValue == "" && entry.NewValue != ""
								.edit-log-add
									RawIcon("plus-circle")
							else if entry.OldValue != "" && entry.NewValue == ""
								.edit-log-remove
									RawIcon("minus-circle")
							else
								.edit-log-change
									RawIcon("pencil")
					td
						.edit-log-user
							Avatar(entry.User())
					td.edit-log-object
						a(href="/" + strings.ToLower(entry.ObjectType) + "/" + entry.ObjectID, target="_blank")= arn.GetObjectTitle(entry.ObjectType, entry.ObjectID)
					td.edit-log-type= entry.ObjectType
					td.edit-log-key= entry.Key
					td= entry.OldValue
					td= entry.NewValue
					td.edit-log-date.utc-date(data-date=entry.Created)