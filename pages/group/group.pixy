component Group(group *arn.Group, user *arn.User)
	GroupTabs(group, user)
	
	if group.Name != ""
		h1.mountable= group.Name
	else
		h1.mountable untitled

	.group-view
		.group-sidebar.mountable
			if group.Description != ""
				.group-sidebar-section
					h3 Description
					.group-description!= markdown.Render(group.Description)

			if group.Rules != ""
				.group-sidebar-section
					h3 Rules
					.group-rules!= markdown.Render(group.Rules)

			.group-sidebar-section
				h3 Members
				.user-avatars.group-members
					each member in group.Members
						Avatar(member.User())
		
		.group-feed.mountable
			if user != nil
				NewPostArea(user, "Content")
			
				.buttons
					button.action(data-action="newGroupPost", data-trigger="click")
						Icon("check")
						span Publish

			each post in group.Posts()
				p!= post.HTML()

component GroupTabs(group *arn.Group, user *arn.User)
	.tabs
		Tab("Group", "users", group.Link())
		//- Tab("Forum", "comment", group.Link() + "/forum")

		if user != nil && group.FindMember(user.ID) != nil && group.FindMember(user.ID).Role == "founder"
			Tab("Edit", "pencil", group.Link() + "/edit")