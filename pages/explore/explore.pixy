component ExploreAnime(animeList []*arn.Anime, year string, status string, typ string, user *arn.User)
	ExploreFilters(year, status, typ, "filterAnime")

	.corner-buttons-hide-on-mobile
		if user != nil
			button.action(data-trigger="click", data-action="hideAddedAnime", title="Hide anime in my collection")
				RawIcon("eye-slash")
		
		a.button.ajax(href="/explore/color/any/anime", title="View colors")
			RawIcon("paint-brush")
		
		a.button.ajax(href="/genres", title="View genres")
			RawIcon("clone")

	h1.page-title Explore

	.explore-anime
		if len(animeList) == 0
			p.no-data.mountable No anime found using the above filters.
		else
			AnimeGrid(animeList, user)

component ExploreFilters(year string, status string, typ string, action string)
	.explore-filters
		select#filter-year.action(value=year, data-action=action, data-trigger="change")
			for year := time.Now().Year()+1; year >= 1951; year--
				option(value=year)= year

		select#filter-status.action(value=status, data-action=action, data-trigger="change")
			option(value="current") Current
			option(value="upcoming") Upcoming
			option(value="finished") Finished

		select#filter-type.action(value=typ, data-action=action, data-trigger="change")
			option(value="tv") TV
			option(value="movie") Movie
			option(value="ova") OVA
			option(value="ona") ONA
			option(value="special") Special