component AnimeListItem(viewUser *arn.User, item *arn.AnimeListItem, anime *arn.Anime)
	.widgets.mountable
		.widget.anime-list-item-view(data-api="/api/animelist/" + viewUser.ID + "/update/" + anime.ID)
			h2= anime.Title.Canonical

			InputNumber("Episodes", float64(item.Episodes), "Episodes", "Number of episodes you watched", "0", arn.EpisodeCountMax(anime.EpisodeCount), "1")
			
			label(for="Status") Status:
			select.widget-element.action(id="Status", data-field="Status", value=item.Status, data-action="save", data-trigger="change")
				option(value=arn.AnimeListStatusWatching) Watching
				option(value=arn.AnimeListStatusCompleted) Completed
				option(value=arn.AnimeListStatusPlanned) Plan to watch
				option(value=arn.AnimeListStatusHold) On hold
				option(value=arn.AnimeListStatusDropped) Dropped

			.anime-list-item-rating-edit
				InputNumber("Rating.Overall", item.Rating.Overall, item.OverallRatingName(), "Overall rating on a scale of 0 to 10", "0", "10", "0.1")
				InputNumber("Rating.Story", item.Rating.Story, "Story", "Story rating on a scale of 0 to 10", "0", "10", "0.1")
				InputNumber("Rating.Visuals", item.Rating.Visuals, "Visuals", "Visuals rating on a scale of 0 to 10", "0", "10", "0.1")
				InputNumber("Rating.Soundtrack", item.Rating.Soundtrack, "Soundtrack", "Soundtrack rating on a scale of 0 to 10", "0", "10", "0.1")
			
			InputNumber("RewatchCount", float64(item.RewatchCount), "Rewatched", "How often you rewatched this anime", "0", "100", "1")
			
			InputTextArea("Notes", item.Notes, "Notes", "Your notes")

			.buttons.mountable
				a.ajax.button(href="/+" + viewUser.Nick + "/animelist")
					Icon("list")
					span View collection
				a.ajax.button(href=anime.Link())
					Icon("search-plus")
					span View anime
				button.action(data-action="removeAnimeFromCollection", data-trigger="click", data-anime-id=anime.ID, data-user-id=viewUser.ID, data-user-nick=viewUser.Nick)
					Icon("trash")
					span Remove from collection